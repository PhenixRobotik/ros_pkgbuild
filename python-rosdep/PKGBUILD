# Maintainer: Salamandar <felix@piedallu.me>

pkgname='python-rosdep-git'
pkgdesc="rosdep package manager abstraction tool for ROS"
url="http://wiki.ros.org/rosdep"
license=('BSD')
pkgver=0.15.1.r0.gfe63aaa
pkgrel=1

depends=(
  'python'
  'python-catkin-pkg'
  'python-rosdistro'
  'python-rospkg'
  'python-yaml'
)
makedepends=(
  'python-setuptools'
)
conflicts=('python-rosdep')
provides=('python-rosdep')
arch=('any')
source=(
  'git+https://github.com/ros-infrastructure/rosdep'
  'install'
)
sha256sums=(
  'SKIP'
  '482819bb3d98b718d0e162d14bc1f1fa51db07d6933e8d6c5ea67bb0ff212ee0'
)

install='install'

pkgver() {
  cd "rosdep"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "rosdep"
  python setup.py build
}

package() {
  cd "rosdep"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
